" See the help for `defaults.vim`.  TODO: maybe only copy the "good" parts from
" `defaults.vim`.
unlet! skip_defaults_vim
source $VIMRUNTIME/defaults.vim

set nocompatible

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Setup Vim for editing in utf-8.
" https://stackoverflow.com/q/5477565
" https://superuser.com/q/154491
" https://stackoverflow.com/q/2438021
if has('multi_byte')
   " Better default than latin1.  Can't be changed after startup.
   if has('vim_starting') | set encoding=utf-8 | endif
   if &termencoding == ''
      let &termencoding = &encoding
   endif
endif
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set mouse=a       " Enable use of the mouse in all modes.
set ruler         " Show the ruler.
set showcmd       " Why does this default to off for Unix ONLY?
set history=10000 " Vim's default: 50.  Neovim's default: 10000.
set incsearch     " Search while typing the search command and...
set hlsearch      " ...hightlight matches.

set shortmess-=S
set shortmess+=F

let &t_SI = "\e[6 q"
let &t_EI = "\e[2 q"

set maxmem=2000000    " Lots of memory for each buffer.
set maxmemtot=2000000 " Lots of memory for all buffers together.

if has('unix')
   source ~/.vim/common.vim
elseif has('win32')
   source ~/vimfiles/common.vim
endif

" TODO: explain.
if has('unix')
   let s:vimfiles = $HOME . '/.vim'
elseif has('win32')
   " Use $HOME or $USERPROFILE?
   let s:vimfiles = $HOME . '/vimfiles'
endif
if exists('s:vimfiles')
   if !isdirectory(s:vimfiles . '/swp')
      call mkdir(s:vimfiles . '/swp', 'p')
   endif
   if !isdirectory(s:vimfiles . '/undo')
      call mkdir(s:vimfiles . '/undo', 'p')
   endif
   if !isdirectory(s:vimfiles . '/backup')
      call mkdir(s:vimfiles . '/backup', 'p')
   endif
   let &dir = s:vimfiles . '/swp//'
   let &undodir = s:vimfiles . '/undo'
   let &backupdir = s:vimfiles . '/backup'
endif
" https://stackoverflow.com/questions/1549263/how-can-i-create-a-folder-if-it-doesnt-exist
" https://vim.wikia.com/wiki/Automatically_create_tmp_or_backup_directories

" If running inside screen, use these escape sequences to name the window (set the title
" of the VT100 emulated by screen).
if &term =~# 'screen'
   set t_ts=k
   set t_fs=\
endif " The settings for those termcap codes are taken from
" https://vim.wikia.com/wiki/Automatically_set_screen_title.

if exists('&belloff')
   set belloff=all
endif

" Fallback method to disable beeping in case the 'belloff' option doesn't exist or doesn't
" work.  See :h 'vb' and :h exists().
if !exists('+belloff')
   set visualbell
   set t_vb=
endif

" I copied this from sensible.vim [1].  Neovim doesn't need it [2].
if has('autocmd')
   filetype plugin indent on
endif
if has('syntax') && !exists('g:syntax_on')
   syntax enable
endif
" [1]: https://github.com/tpope/vim-sensible/blob/master/plugin/sensible.vim
" [2]: https://github.com/neovim/neovim/issues/2676
